*** Settings ***
Suite Setup                   Get Test Cases

*** Variables ***
${repls_path}=                ${CURDIR}/../ci-output/repls
${pattern}=                   *.repl.diff

*** Keywords ***
Get Test Cases
    Setup
    # List Files In Directory returns the names of the repls, not full paths
    @{platforms}=             List Files In Directory  ${repls_path}/diffs  ${pattern}
    ${list_length}=           Get Length  ${platforms}
    # The CI script should have skipped running Renode if there are no repls with diffs
    Should Not Be True        ${list_length} == 0
    Set Suite Variable        ${platforms}

Try Load Platform
    [Arguments]               ${arg}
    ${repl_path}=             Remove String  ${arg}  .diff
    Execute Command           mach create
    ${result}=                Run Keyword And Ignore Error  Execute Command  machine LoadPlatformDescription "${repl_path}"
    Reset Emulation
    Return From Keyword       ${result}

Check If New Repl Has Not Regressed Loading
    [Arguments]               ${arg}
    ${repl_name}=             Remove String  ${arg}  .diff

    ${old_repl_path}=         Set Variable  ${repls_path}/dashboard/${repl_name}
    ${new_repl_path}=         Set Variable  ${repls_path}/generated/${repl_name}
    ${old_result}=            Try Load Platform  ${old_repl_path}
    ${new_result}=            Try Load Platform  ${new_repl_path}
    ${old_size}=              Evaluate  os.stat("${old_repl_path}").st_size if os.path.exists("${old_repl_path}") else 0

    ${color_red}=             Evaluate  "\\033[1;31m"
    ${color_green}=           Evaluate  "\\033[1;32m"
    ${color_reset}=           Evaluate  "\\033[0m"

    # We use Log To Console for the messages to avoid "Message content over the limit has been removed"
    IF  ${old_size} == 0 and "${new_result[0]}" == "PASS"
        # Since dts2repl-generated repls will always contain at least `// autogenerated`, if
        # a repl is empty here, we know it didn't exist and we filled it in with a blank one.
        Log To Console        [${color_green}+${color_reset}] ${repl_name} didn't exist, but now does and loads!
    ELSE IF  ${old_size} == 0 and "${new_result[0]}" == "FAIL"
        Log To Console        [~] ${repl_name} didn't exist, but now does, but doesn't load, error is ${new_result[1]}
    ELSE IF  "${old_result[0]}" == "PASS" and "${new_result[0]}" == "FAIL"
        Log To Console        [${color_red}-${color_reset}] ${repl_name} used to load, but now it doesn't, new error is ${new_result[1]}
        Fail                  ${repl_name}
    ELSE IF  "${old_result[0]}" == "FAIL" and "${new_result[0]}" == "PASS"
        Log To Console        [+] ${repl_name} didn't use to load, but now it does!
    END

*** Test Cases ***
Should Load Repls
    # This tests uses templates as it tests every item on the list, even if a prior one failed
    # and produces an aggregated failure summary
    [Template]  Check If New Repl Has Not Regressed Loading
    FOR  ${test}  IN  @{platforms}
        ${test}
    END
